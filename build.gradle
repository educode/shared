plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.freefair.lombok' version '3.8.1'
}

group 'de.hhu.educode'
version '1.0.8' + (Boolean.valueOf(System.getProperty('release')) ? '' : '-SNAPSHOT')

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.nimbusds:nimbus-jose-jwt:7.8'
    implementation 'org.slf4j:slf4j-api:1.7.28'

    compileOnly 'io.grpc:grpc-api:1.23.0'
    compileOnly 'io.projectreactor:reactor-core:3.2.10.RELEASE'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.assertj:assertj-core:3.13.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply from: 'publish.gradle'
apply from: 'bintray.gradle'

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '5.6.2'
}
